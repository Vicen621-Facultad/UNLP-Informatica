programa Ejer1
procesos
  proceso JuntarFlores(ES flores: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores := flores + 1
  fin
  proceso JuntarPapeles(ES papeles: numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      papeles := papeles + 1
  fin
  proceso EnviarIDs
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, a1)
    EnviarMensaje(4, a2)
  fin
  proceso EnviarMensajes(E id: numero; E val: numero)
  comenzar
    EnviarMensaje(id, fiscal)
    EnviarMensaje(val, fiscal)
  fin
  proceso RecibirMensajeId(E id: numero; ES data: numero)
  comenzar
    si id = 1
      RecibirMensaje(data, r1)
    sino
      si id = 2
        RecibirMensaje(data, r2)
      sino
        si id = 3
          RecibirMensaje(data, a1)
        sino
          RecibirMensaje(data, a2)
  fin
areas
  av2parte1: AreaP(2, 1, 2, 97)
  av2parte2: AreaP(2, 100, 2, 100)
  av3parte1: AreaP(3, 1, 3, 97)
  av3parte2: AreaP(3, 100, 3, 100)
  ca98parte1: AreaP(1, 98, 1, 98)
  ca98parte2: AreaP(4, 98, 100, 98)
  ca99parte1: AreaP(1, 99, 1, 99)
  ca99parte2: AreaP(4, 99, 100, 99)
  esquinasPC: AreaPC(2, 98, 3, 99)
  aFiscal: AreaP(1, 1, 1, 1)
robots
  robot rojo
  variables
    flores, id: numero
  comenzar
    RecibirMensaje(id, fiscal)
    flores := 0
    repetir 96
      JuntarFlores(flores)
      mover
    JuntarFlores(flores)
    BloquearEsquina(PosAv, 98)
    mover
    JuntarFlores(flores)
    BloquearEsquina(PosAv, 99)
    mover
    LiberarEsquina(PosAv, 98)
    JuntarFlores(flores)
    mover
    LiberarEsquina(PosAv, 99)
    JuntarFlores(flores)
    EnviarMensajes(id, flores)
  fin
  robot azul
  variables
    papeles, id: numero
  comenzar
    RecibirMensaje(id, fiscal)
    papeles := 0
    derecha
    BloquearEsquina(2, PosCa)
    mover
    JuntarPapeles(papeles)
    BloquearEsquina(3, PosCa)
    mover
    LiberarEsquina(2, PosCa)
    JuntarPapeles(papeles)
    mover
    LiberarEsquina(3, PosCa)
    repetir 96
      JuntarPapeles(papeles)
      mover
    JuntarPapeles(papeles)
    EnviarMensajes(id, papeles)
  fin
  robot rFiscal
  variables
    papeles, flores, id, data: numero
  comenzar
    EnviarIDs
    papeles := 0
    flores := 0
    repetir 4
      RecibirMensaje(id, *)
      RecibirMensajeId(id, data)
      si ((id = 1) | (id = 2))
        flores := flores + data
      sino
        papeles := papeles + data
    si (flores > papeles)
      Informar('rojo', flores)
    sino
      Informar('azul', papeles)
  fin
variables
  r1, r2: rojo
  a1, a2: azul
  fiscal: rFiscal
comenzar
  AsignarArea(r1, av2parte1)
  AsignarArea(r1, av2parte2)
  AsignarArea(r1, esquinasPC)
  AsignarArea(r2, av3parte1)
  AsignarArea(r2, av3parte2)
  AsignarArea(r2, esquinasPC)
  AsignarArea(a1, ca98parte1)
  AsignarArea(a1, ca98parte2)
  AsignarArea(a1, esquinasPC)
  AsignarArea(a2, ca99parte1)
  AsignarArea(a2, ca99parte2)
  AsignarArea(a2, esquinasPC)
  AsignarArea(fiscal, aFiscal)
  Iniciar(r1, 2, 1)
  Iniciar(r2, 3, 1)
  Iniciar(a1, 1, 98)
  Iniciar(a2, 1, 99)
  Iniciar(fiscal, 1, 1)
fin
